      * Curl MIME post data and display response
      *
     h DFTACTGRP(*NO) ACTGRP(*NEW)
     h OPTION(*NOSHOWCPY)
     h BNDDIR('FETCH')
      *
      **************************************************************************
      *                                  _   _ ____  _
      *  Project                     ___| | | |  _ \| |
      *                             / __| | | | |_) | |
      *                            | (__| |_| |  _ <| |___
      *                             \___|\___/|_| \_\_____|
      *
      * Copyright (C) Daniel Stenberg, <daniel@haxx.se>, et al.
      *
      * This software is licensed as described in the file COPYING, which
      * you should have received as part of this distribution. The terms
      * are also available at https://curl.se/docs/copyright.html.
      *
      * You may opt to use, copy, modify, merge, publish, distribute and/or sell
      * copies of the Software, and permit persons to whom the Software is
      * furnished to do so, under the terms of the COPYING file.
      *
      * This software is distributed on an "AS IS" basis, WITHOUT WARRANTY OF
      * ANY KIND, either express or implied.
      *
      * SPDX-License-Identifier: fetch
      *
      **************************************************************************
      *
      /include H,FETCH.INC
      *
      * Example to HTTP POST data using the MIME API. Displays the response.
      *
     d                 pi
     d userinput                    120                                         User data to post
      *
     d url             c                   'http://httpbin.org/anything'
      *
      *
     d inputlen        s             10u 0                                      User input length
      **************************************************************************

        inputlen = trimmed_length(userinput: %len(userinput));

        // Do the fetch stuff.

        fetch_global_init(FETCH_GLOBAL_ALL);
        main();
        fetch_global_cleanup();
        *inlr = *on;            // Exit
      *
      **************************************************************************
      * Main procedure: do the fetch job.
      **************************************************************************
      *
     p main            b
     d main            pi
      *
     d h               s               *                                        Easy handle
     d result          s                   like(FETCHcode)                       Curl return code
     d                                     inz(FETCHE_OUT_OF_MEMORY)
     d errmsgp         s               *                                        Error string pointer
     d response        s             52                                         For error display
     d mime            s               *                                        MIME handle
     d mimepart        s               *                                        MIME part handle
     d parthdrs        s               *   inz(*NULL)                           Part headers

            // Create and fill fetch handle.

        h = fetch_easy_init();
        if h <> *NULL;
            fetch_easy_setopt_ccsid(h: FETCHOPT_URL: url: 0);
            fetch_easy_setopt(h: FETCHOPT_FOLLOWLOCATION: 1);
            mime = fetch_mime_init(h);
            mimepart = fetch_mime_addpart(mime);
            fetch_mime_name_ccsid(mimepart: 'autofield': 0);
            fetch_mime_data_ccsid(mimepart: 'program-generated value':
                                 FETCH_ZERO_TERMINATED: 0);
            mimepart = fetch_mime_addpart(mime);
            fetch_mime_name_ccsid(mimepart: 'userfield': 0);
            fetch_mime_data_ccsid(mimepart: %subst(userinput: 1: inputlen):
                                 FETCH_ZERO_TERMINATED: 0);
            mimepart = fetch_mime_addpart(mime);
            fetch_mime_name_ccsid(mimepart: 'ebcdicfield': 0);
            fetch_mime_data(mimepart: %subst(userinput: 1: inputlen): inputlen);
            fetch_mime_encoder_ccsid(mimepart: 'base64': 0);
            // Avoid server to convert base64 to text.
            parthdrs = fetch_slist_append_ccsid(parthdrs:
                                       'Content-Transfer-Encoding: bit': 0);
            fetch_mime_headers(mimepart: parthdrs: 1);
            fetch_easy_setopt(h: FETCHOPT_MIMEPOST: mime);

            // Perform the request.

            result = fetch_easy_perform(h);
            fetch_mime_free(mime);
            fetch_easy_cleanup(h);       // Release handle
        endif;

        // Check for error and report if some.

        if result <> FETCHE_OK;
            errmsgp = fetch_easy_strerror_ccsid(result: 0);
            response = %str(errmsgp);
            dsply '' '*EXT' response;
        endif;
     p main            e
      *
      **************************************************************************
      * Get the length of right-trimmed string
      **************************************************************************
      *
     p trimmed_length  b
     d trimmed_length  pi            10u 0
     d  string                   999999    const options(*varsize)
     d  length                       10u 0 value
      *
     d len             s             10u 0
      *
        len = %scan(X'00': string: 1: length); // Limit to zero-terminated string
        if len = 0;
            len = length + 1;
        endif;
        if len <= 1;
            return 0;
        endif;
        return %checkr(' ': string: len - 1);  // Trim right
     p trimmed_length  e
